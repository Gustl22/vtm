apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'

dependencies {
    compile project(':vtm')
    compile 'com.caverock:androidsvg:1.2.2-beta-1'
}

android {
    compileSdkVersion androidCompileSdk()
    buildToolsVersion = "$androidBuildVersionTools"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    defaultConfig {
        versionCode versionCode()
        versionName versionName()
        minSdkVersion androidMinSdk()
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }
        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }

    lintOptions { abortOnError false }
}

android.libraryVariants.all { variant ->
    def name = variant.buildType.name
    if (name.equals("debug")) {
        print "Skipping debug jar"
        return; // Skip debug builds.
    }

    def taskJar = project.tasks.create "jar${name.capitalize()}", Jar
    taskJar.dependsOn variant.javaCompile
    taskJar.from variant.javaCompile.destinationDir
    artifacts.add('archives', taskJar);

    file('natives').eachDir() { dir ->
        def taskNatives = project.tasks.create "natives_${dir.name}_Jar${name.capitalize()}", Jar
        taskNatives.classifier = "natives-${dir.name}"
        taskNatives.from(dir.path)
        artifacts.add('archives', taskNatives);
    }

    def taskFat = project.tasks.create "fatJar${name.capitalize()}", Jar
    taskFat.classifier = 'jar-with-dependencies'
    taskFat.dependsOn variant.javaCompile
    taskFat.from variant.javaCompile.destinationDir
    taskFat.from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    file('natives').eachDir() { dir ->
        taskFat.from(dir.path)
    }
    artifacts.add('archives', taskFat);
}

// Automated Gradle project deployment to Sonatype OSSRH
if (isReleaseVersion && project.hasProperty("SONATYPE_USERNAME")) {
    afterEvaluate {
        project.apply from: "${rootProject.projectDir}/deploy.gradle"
    }
}
