apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'application'

dependencies {
    compile project(':vtm-gdx')
    compile files('natives')
    compile "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
    compile "com.badlogicgames.gdx:gdx-backend-jglfw:$gdxVersion"
    compile 'com.kitfox.svg:svg-salamander:1.0'
    compile 'org.slf4j:slf4j-simple:1.7.21'
}

sourceSets {
    main {
        java.srcDirs = ['src']
    }
}

task nativesJar(type: Jar) {
    classifier = 'natives'
    from('natives')
}

task fatJar(type: Jar, dependsOn: classes) {
    classifier = 'jar-with-dependencies'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    from('natives')
    exclude 'META-INF/*.DSA', 'META-INF/*.RSA', 'META-INF/*.SF'
    with jar
}

artifacts {
    archives nativesJar
    archives fatJar
}

mainClassName = 'org.oscim.gdx.GdxMapApp'
run { ignoreExitValue = true }

// Automated Gradle project deployment to Sonatype OSSRH
if (isReleaseVersion && project.hasProperty("SONATYPE_USERNAME")) {
    afterEvaluate {
        project.apply from: "${rootProject.projectDir}/deploy.gradle"
    }
}
